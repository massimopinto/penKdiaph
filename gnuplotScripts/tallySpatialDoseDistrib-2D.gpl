# gnuplot script file for penEasy
# Last update:
#   2014-12-12 by JS

# Factor from eV to keV:
f = 1.0e-3

################
# 2D Map plots #
################

set size ratio -1
# Determine voxel color from corner left-bottom:
set pm3d map corners2color c1 implicit
# In case you want a gray scale plot:
# set palette gray negative

unset key
set title "2D Dose Distribution\n(keV.cm/g per history)"
set xlabel "1st coord (cm)"
set ylabel "2nd coord (cm)"
splot "tallySpatialDoseDistrib-2D.dat" using 2:5:($7*f)
pause -1 "Press OK"

#################
# Surface plots #
#################

reset
set pm3d hidden3d 1 implicit
unset surface
set contour

set style line 1 linetype -1 linewidth 0.3 pointtype 7 pointsize 0.5
unset key
set title "2D Dose Distribution\n(keV.cm/g per history)"
set xlabel "1st coord (cm)" rotate parallel
set ylabel "2nd coord (cm)" rotate parallel
set zlabel "dose" rotate parallel
splot "tallySpatialDoseDistrib-2D.dat" using 2:5:($7*f) with lines
pause -1 "Press OK"

# EOF

