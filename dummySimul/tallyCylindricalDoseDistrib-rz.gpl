# gnuplot script file for penEasy
# Last update:
#   2014-12-12 by JS

# Factor from eV to keV:
f = 1.0e-3

################
# 2D Map plots #
################

set size ratio -1
# Determine voxel color from corner left-bottom:
set pm3d map corners2color c1 implicit
# In case you want a gray scale plot:
# set palette gray negative

unset key
set title "Cylindrical Dose Distribution\n(keV/g per history)"
set xlabel "radius (cm)"
set ylabel "z (cm)"
splot "tallyCylindricalDoseDistrib-rz.dat" using 2:5:($7*f)
pause -1 "Press OK"

#################
# Surface plots #
#################

reset
set pm3d hidden3d 1 implicit
unset surface
set contour

set style line 1 linetype -1 linewidth 0.3 pointtype 7 pointsize 0.5
unset key
set title "Cylindrical Dose Distribution\n(keV/g per history)"
set xlabel "radius (cm)" rotate parallel
set ylabel "z (cm)" rotate parallel
set zlabel "dose" rotate parallel
splot "tallyCylindricalDoseDistrib-rz.dat" using 2:5:($7*f) with lines
pause -1 "Press OK"

##############
# Line plots #
##############

reset
# Pause interval (-1 stops execution):
frameRate = -1

set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 0.5
set linestyle 1
unset key
set title "Cylindrical Dose Distribution"
set xlabel "radius (cm)"
set ylabel "dose (keV/g per history)"

do for [i=1:30000]{
  plot [0:] [0:] "tallyCylindricalDoseDistrib-rz.dat" using 3:($4==i?($7*f):-1):($8*f) with yerrorbars linestyle 1, \
                 "tallyCylindricalDoseDistrib-rz.dat" using 2:($4==i?($7*f):-1) with steps linestyle 1
  pause frameRate sprintf("z-slice index = %i. Press OK",i)
}

# EOF
