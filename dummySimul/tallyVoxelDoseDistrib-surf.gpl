# gnuplot script file for penEasy
# Last update:
#   2014-12-12 by JS

pause -1 "z slices are automatically displayed in sequence. For manual, set frameRate in script to -1."

# Pause interval (use -1 to stop execution after each frame):
frameRate = 0.2

# Factor from eV to keV:
f = 1.0e-3

set pm3d hidden3d 1 implicit
unset surface
set contour
# In case you want a gray scale plot:
# set palette gray negative

set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 0.5
unset key
set title "3D Dose Distribution\n(keV/g per history)"
set xlabel "x (cm)" rotate parallel
set ylabel "y (cm)" rotate parallel
set zlabel "dose" offset -5 rotate parallel

set zrange [0:]

do for [i=0:30000]{
  set label 2 sprintf("z voxel index = %i",i+1) at screen 0.5,0.8 center
  splot "tallyVoxelDoseDistrib.dat" index i using 1:3:($7*f) with lines
  pause frameRate "Press OK"
}

# EOF
